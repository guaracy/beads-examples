#beads level 1 module store_admin

const
	TRACE_PARSE = T
	
-------------------
//   main model
-------------------
enum
	PHASE_LOGIN
	PHASE_MAIN

const
	INDENT_L = 20

	GRAD_HEADER : tree = gradient_load (GRAD_SHAPE_LINEAR, 0, GRAD_TO_BOTTOM, 0, 0x7b1c8e, 100, 0x2a1b33)
	GRAD_BUTTON : tree = gradient_load (GRAD_SHAPE_LINEAR, 0, GRAD_TO_BOTTOM, 0, 0x7ab3e2, 40, 0x4a8ece, 100, 0x1b60c6)

/*....
//  there are multiple servers holding the menu
record a_server
	signature : str   // signature to allow communication
	ip     : str   //  ip address
	port   : num   //  port number
	active : num   //  T if should be used
	seq    : num   //  sequence number this server is up to
	ping_n : num   //  number of times we got back a ping
	ping_cum : num  //  cumulative of ping times
...*/
	
var	
	//  transient user interface variables
	intf : record
		curr_mode  : num
		restaurant : str
		ftp_user   : str
		ftp_pass   : str
		picked_files : array [index] of str
		messages   : array[index] of str
		
	//  persistent preferences
	pref : record
		menu_file  : str  //  local path name to the specification to load

	g_nmissing_photos : num
	
//  note: this definition must match the code in the store client
//  the restaurant menu, which is a 2 level hierarchy, of categories and the items in that category
//  note that the menu is dynamically downloaded at startup if out of date
var menu  : record
	version  : num  `version number of this menu form`
	release  : num  `revision number of the menu`  // used to detect if a new menu is available

	//  we can fit up to 12 menu categories on the home screen picker
	categories : array [index] of record        `array of menu categories like "dessert"`
		cat_name     : array [language] of str  `localized name of the category`
		cat_pic      : pic                      `icon for the category`
		cat_picname  : str
		cat_items    : array [index] of num     `the itemID's in this category` 
 
	//  note that because the category refers to items by id,
	//  it is legal to have the same menu item in multiple categories
	items : array [itemID] of record  `array of menu items`
		item_pic        : pic                        `picture of menu item if available`
		item_picname    : str                        `file name of picture if there is one`
		item_fincode    : str                        `financial code`
		item_flags      : str                        `flags like: GLUT|NUTS|SHEL|SHRI|NTAX|VEGN| etc.`
		item_cook_time  : num                        `avg number of minutes it takes to prepare the item`
		item_default_size : num                        `default size index`
		item_sizes      : array [index] of str       `list of sizes like "small" (not localized)`
		item_prices     : array [index] of num       `price at this size`
		item_desc_short : array [language] of str    `short description, localized`
		item_desc_long  : array [language] of str    `long description, localized`
	//..end items
//..end menu


-------------------FIX because we don't have auto import of str yet
calc split_lines_words `split a string into 2D array indexed [lines, words]` #LOC (
	ss         : str   `starting string`
	dest       : path  `destination address for array of lines and words`
	word_delim : str   `word break character like tab or comma`
	quotes     : num = T  `if true, then consider double quotes to allow embedding of LF, strip them`	
	)
	return
	//...end dummy func
calc str_sub `string subset` (
	ss       : str	`starting string`
	from     : num ⇐ FIRST `starting position to take from`
	to       : num ⇐ U `ending position (inclusive)`
	len      : num ⇐ U `number of characters to extract`
	rev      : num ⇐ F `count from end?`	
	)        : str `extracted string`
	return "xx"
calc str_begins `see if string starts with a pattern` (
	ss   : str `big string to check against`
	pattern : str `pattern to look for`
	) : num  `T, F, U, or ERR`
calc str_to_enum ( `convert a string of an enum into its internal value`
	s : str	`enum string`
	) : num	`internal representation of that enum`
	return 0
calc str_to_num ( `convert a string containing a number into a number` 
	s : str `string to convert`
	) : num `number form (ERR if bad string)`
	return 0
calc str_strip_quotes `strip quotes around a string` (
	ss : str	`string to process`
	)  : str	`stripped version`
	return "xx"
calc str_replace `replace a pattern in a string` #LOC (
	haystack : str     `haystack to search`
	needle   : str     `needle to find`
	replace  : str     `replacement pattern to use`
	list     : tree ⭅ {} `optional array to fill with an array of a_find, shows ending positions`
	from     : num ⇐ FIRST `starting position`
	limit    : num ⇐ 1 `repetition limit`
	regular  : num ⇐ F `patterns are regular expressions?`
	rev      : num ⇐ F `count starting pos (and perform search) going backwards?`
	no_case  : num ⇐ F `case insensitive flag, T=ignore case`
	wrap     : num ⇐ F `wrap around search`
	)        : str `modified string`
	return "xx"
calc split_words `split a line into words`  #LOC  (
	ss		  : str    `starting string`
	dest	  : path    `destination address for array of words`
	delimiter : str    `word break character`
	)
	return
//...end fix
	
-------------------
calc msg ( `append to our log file`
	text : str `line to append to log file`
	)
-------------------
	text append=> intf.messages
	if TRACE_PARSE
		log(text)
	
-------------------
calc look_for_photo (
	must : num   `T if must find photo else error`
	myname : str   `name of photo`
	dest : path  `destination str node to store into`
	)
-------------------
	if myname == ''
		return	//  this item doesn't have a pic
	
/*.... see as3 code for actual code	
	//  form the local path of the image
	//  then see if that imate file exists.
	//  if it does exist, then store the name into the dest pic
	//  
	var mypath : str = 'photos/' & myname & '.jpg'
	if file_exists(mypath)
		//  file exists, store this info
		dest! = myname

		//  append to our list of files to load
		var ix : num = tree_next_hi(resources)
		resources[ix].localpath = mypath 
	elif must
		//  if we must have an existing file, this is an error
		log ("error - required picture doesn't exist {myname}")
....*/
		
-------------------
calc parse_menu_file : num  //  return T if okay
-------------------
	const
		COL_LEVEL    = 1
		COL_COOKTIME = 2
		COL_FLAGS    = 3
		COL_SIZE     = 4
		COL_PRICE    = 5
		COL_ID       = 6   // unique ID code for level 2 items, convert to enum
		COL_PHOTO    = 7
		COL_LANG_ENG = 8   // must be ENG|SHORT
	
		ROW_VERSION = 1
		ROW_HEADERS = 2    // which row contains the column headers
		
	var
		ss     : str
		ver_ss : str
		desc   : str
		code   : str
		langs  : array [index] of num
		prices : array [index] of str
		sheet  : array [row, col] of str
		status : num
		colx   : num
		langID : num
		itemID : num
		groupx : num
		rowx   : num
		nminutes : num
		nsizes : num
		nprices : num
		price  : num
		row_level : num
		num_in_group : num
		defaultx : num

	//  we keep track of how many missing photos we have
	g_nmissing_photos = 0

			//  version 1
	menu.version = 1
	
	//  not currently updating this field
	menu.release = 1

	//  read the CSV file containing the menu in the form of spreadsheet
	ss = file_read_str (pref.menu_file)
	if ss == U
		msg("Abort: file missing or permissions problem")
		return F

	split_lines_words (ss, sheet, "\t", quotes:T)
	ver_ss = sheet[ROW_VERSION, 1]
	if ver_ss != "v1"
		msg("Abort: unsupported version {ver_ss}")
		return F

	//  [1,2]: restaurant title
	//  [1,3]: the userid for FTP
	//  [1,4]: the pass for FTP
	intf.restaurant = sheet[1, 2]
	intf.ftp_user = sheet[1, 3]
	intf.ftp_pass = sheet[1, 4]

	//  check a few of the headers for sanity
	if not str_begins(sheet[ROW_HEADERS, COL_LEVEL], "LEVEL") or (sheet[ROW_HEADERS, COL_LANG_ENG] != "ENG|SHORT")
		msg("Abort: bad headers")
		return F

	//  we just checked for the existence of english, so that is our first language
	//  look for other languages in the column headers
	//  languages are in pairs, ENG|SHORT, ENG|LONG, ESP|SHORT, ESP|LONG etc.
	langs[1] = LANG_ENG
	colx = COL_LANG_ENG
	loop
		colx =+ 2
		ss = sheet[ROW_HEADERS, colx]
		//  a language prefixed by # means stop parsing languages
		if (ss == U) or (str_sub(ss, from:FIRST, len:1) == '#')
			break
		code = str_sub(ss, from:1, len:3)
		//  create a string LANG_xxx where xxx is the language code in the spreadsheet
		ss = "LANG_" & code
		//  append to our array
		str_to_enum(ss) append=> langs
	// end loop

	if TRACE_PARSE
		log("...total number of valid languages is {tree_count(langs)}")

	//  now build the menu items
	groupx = 0

	loop from:ROW_HEADERS+1 to:tree_hi(sheet) index:rowx
		if TRACE_PARSE
			log("--processing row {rowx}")
		ss = sheet[rowx, COL_LEVEL]
		if (ss == U) or (str_sub(ss, from:1, len:1) == '=')
			//  ignore lines with no level specified, or lines with '='
			continue
			
		//  convert level string to number
		row_level = str_to_num(ss)
		case row_level
		| 1
			//-----------------
			//   Category row
			//-----------------
			groupx =+ 1
			num_in_group = 0
			ss = sheet[rowx, COL_PHOTO]
			look_for_photo (T, ss, menu.categories[groupx].cat_picname)
			
			colx = COL_LANG_ENG  //  short name, english
			loop across:langs val:langID index:i
				ss = sheet[rowx, colx]
				menu.categories[groupx].cat_name[langID] = ss
				colx =+ 2
			// end loop
			
		| 2
			//-----------------
			//   Item row
			//-----------------
			num_in_group =+ 1

			//  convert the unique financial ID into an enum
			//  if there is no financial ID, then use the photo name
			ss = sheet[rowx, COL_ID]
			itemID = str_to_enum(ss)

			//  add this menu item
			itemID append=> menu.categories[groupx].cat_items

			ss = sheet[rowx, COL_PHOTO]
			look_for_photo (F, ss, menu.items[itemID].item_picname)

			//  store the language variants of the item description
			colx = COL_LANG_ENG
			loop across:langs val:langID index:i
				desc = sheet[rowx, colx]
				colx =+ 1
				if desc != U
					//  because the short description has to fit on few lines
					//  to control word-wrap more precisely, the menu admin can put tilde
					//  characters in the string, which we convert to non break spaces
					ss = str_replace (desc, '~', '\u00A0', limit:INFINITY)
					//ss = str_strip_quotes (ss)
					menu.items[itemID].item_desc_short[langID] = ss
				
				//  get the long description if any
				desc = sheet[rowx, colx]
				colx =+ 1
				if desc != U
					menu.items[itemID].item_desc_long[langID] = desc
			// end language loop

			
			//  get the number of minutes to cook, default is 1
			nminutes = 1
			ss = sheet[rowx, COL_COOKTIME]
			if ss != U
				nminutes = str_to_num(ss)
				if nminutes == ERR
					msg("Bad cook time on row {rowx}")
					return F
			menu.items[itemID].item_cook_time = nminutes

			//  get the size strings, this is a splittable string
			//  delimited by vertical bars for the case of multiple sizes
			ss = sheet[rowx, COL_SIZE]
			if ss == U
				nsizes = 1
			else
				//  split into an array of size strings
				split_words (ss, menu.items[itemID].item_sizes, "|")
				nsizes = tree_count(menu.items[itemID].item_sizes)

			//  if the number of sizes is greater than 1, then admin might have marked the menu size with a star,
			//  which indicates that this is the default size for this menu item
			//  we need to look for a marked item, and fix the string
			//  the default size index is the middle size.
			defaultx = 1
			if nsizes >= 2
				//  in the absence of an explicit marker, choose the middle size as the default
				//  this formula will pick the middle size if an odd number
				defaultx = round_down((nsizes + 1)/2)
				loop across:menu.items[itemID].item_sizes val:ss index:sizex
					//  if the size is prefixed by an asterisk, this is the default size
					if str_sub(ss, from:FIRST, len:1) == '*'
						defaultx = sizex
						ss = str_sub(ss, from:FIRST + 1)  //  strip the asterisk
						menu.items[itemID].item_sizes[sizex] = ss  // update the string

			//  we have studied the sizes, now we know the default size 
			menu.items[itemID].item_default_size = defaultx

			//  get the price string, this is a splittable string, like "2.50|5.00" which matches the dimension of the size string list
			ss = sheet[rowx, COL_PRICE]
			if ss == U
				//  empty price not allowed
				msg("Error: empty price not allowed on row {rowx}")
				return F

			//  split the price string into temp array
			if TRACE_PARSE
				log ("splitting price string=[{ss}]")

			//  split the price string into an array
			split_words (ss, prices, "|")

			//  the quantity of prices must match the number of sizes
			nprices = tree_count(prices)
			if nprices != nsizes 
				msg("Error: number of prices ({nprices}) doesn't match the number of sizes ({nsizes}) on row {rowx}")
				return F

			//  now loop through the array of prices and convert them to numeric form
			loop across:prices val:ss index:i
				price = str_to_num (ss)
				if price == ERR
					//  bad input value
					msg("Error: price ({ss}) not numeric on row {rowx}")
					return F
				menu.items[itemID].item_prices[i] = price
			//  end price loop

		| 3
			msg("ignoring level 3")
		
		else
			//  unsupported value
			msg("Error: bad level on row {rowx}")
			return F

		//  end row loop
		
	//  if we get here we had no errors
	msg("Menu parsed okay, {tree_count(menu.items)} menu items processed.")
	return g_nmissing_photos == 0
	//  end func

-------------------
calc main_init
-------------------
	intf.curr_mode = PHASE_MAIN
	intf.restaurant = "Finfine"
	
	pref.menu_file = "/code/order_restaurants/finfine/menu.csv"

-------------------
draw main_draw
-------------------
	//  draw outer frame
	draw_rect (b.box, fill:COLOR_GRAY8, stroke:COLOR_GRAY1, width:4 pt)

	case intf.curr_mode
	| PHASE_LOGIN
		panel_login()

	| PHASE_MAIN
		panel_main()

-------------------
draw panel_login
-------------------
	draw_rect (b.box, fill:COLOR_LIGHT_GREEN)
	draw_str (b.box, ".. login ..", stroke:COLOR_WHITE, size:14 pt)

-------------------
vert panel_main
-------------------
	add 24 pt main_header
	add 80 al main_body

-------------------
draw main_header
-------------------
	draw_rect (b.box, grad:GRAD_HEADER)
	draw_str (b.box, "Monolithic Mobile Restaurant Admin Tool", stroke:COLOR_WHITE, size:b.box.height*0.70)

-------------------
horz main_body
-------------------
	skip INDENT_L pt
	add 100 al main_body2
	skip INDENT_L pt

-------------------
vert main_body2
-------------------
	skip 8 al
	add  24 pt body_store
	skip 8 al
	add  34 pt body_file
	skip 8 al
	add  34 pt submit_row
	skip 8 al
	add  100 al body_status
	skip 8 al

-------------------
horz body_store
-------------------
	var ss: str = "Restaurant:  " & intf.restaurant
	draw_str (b.box, ss, size:b.box.height*0.70, horz:0, stroke:COLOR_BROWN)

-------------------
horz submit_row
-------------------
	//  hide the edit button if we haven't got a file to edit yet
	skip 100 al
	if pref.menu_file != U
		add  50 al butt_submit
	skip 100 al
 
-------------------
horz body_file
-------------------
	//  hide the edit button if we haven't got a file to edit yet
	if pref.menu_file != U
		add  40 al butt_edit
	else
		skip 40 al
	skip 5 al
	add 300 al file_path
	skip 5 al
	add 40 al butt_select
	skip 5 al
 
-------------------
draw butt_edit
-------------------
	draw_rect (b.box, radius:4 pt, grad:GRAD_BUTTON)
	draw_str (b.box, "Edit", stroke:COLOR_WHITE, size:b.box.height*0.60)
-------------------
track
	if (e.evkind == EVKIND_GEST_TAP)
		launch_file(pref.menu_file)
		return T
		
-------------------
draw butt_submit
-------------------
	draw_rect (b.box, radius:4 pt, grad:GRAD_BUTTON)
	draw_str (b.box, "SUBMIT", stroke:COLOR_WHITE, size:b.box.height*0.60)
-------------------
track
	if (e.evkind == EVKIND_GEST_TAP)
		if parse_menu_file() == T  //  process the file
			//  continue processing
			log("phase 2 of menu")
		return T

-------------------
draw butt_select
-------------------
	draw_rect (b.box, radius:4 pt, grad:GRAD_BUTTON)
	draw_str (b.box, "Select", stroke:COLOR_WHITE, size:b.box.height*0.60)
-------------------
track
	if (e.evkind == EVKIND_GEST_TAP)
		//  activate the OS dialog to pick a file, and call the completion function when it is finished
		pick_file_open ("Select menu spreadsheet CSV or TXT file...", file_picker_done, prompt:"Text files", suffixes:"*.csv;*.txt")

-------------------
//  the callback function for the file picker. must have 1 parameter, the array of strings that the user picked
calc file_picker_done ( `callback function when user finishes picking files`
	data : array [index] of str `list of files picked`
	)
-------------------
	if tree_count(data) > 0
		//  user did pick a file
		pref.menu_file = data[1]
		
-------------------
draw file_path
-------------------
	draw_str (b.box, pref.menu_file, size:16 pt, horz:0.5, font:"_typewriter")

-------------------
draw body_status
-------------------
	draw_rect (b.box, radius:10 pt, fill:COLOR_BEIGE)

